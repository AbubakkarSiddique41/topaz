# compstmt: line* statement? [LINE_END]?;
# unfortunately you need to inline this anywhere you want it because ebnf parse
# doesn't support epsilon productions.

IGNORE: "[ ]";
LINE_END: "[\n;]";

NUMBER: "-?[0-9]+(\.[0-9]+)?";
STRING: "\\"[^\\\\"]*\\"";
IDENTIFIER: "[a-zA-Z_][a-zA-Z0-9_]*";

DOT: "\.";
LT: "<";
LPAREN: "\(";
RPAREN: "\)";

ASSIGNMENT_OPER: "(\+=)|(-=)|=";
RANGE_OPER: "(\.\.)|(\.\.\.)";
CMP_OPER: "(!=)|(==)|(<=)|(>=)|>";
SHIFT_OPER: "<<";
ADD_OPER: "[+-]";
MUL_OPER: "[*/]";

main: line* statement? [LINE_END]? [EOF];

line: statement? [LINE_END];

statement: "return" expr |
            expr;

expr: assignment |
      yield |
      arg;

assignment: send ASSIGNMENT_OPER expr;

yield: ["yield"] args?;

arg: range;

cmp_oper: <CMP_OPER> | <LT>;

range: comparison RANGE_OPER range | <comparison>;
comparison: shiftive cmp_oper comparison | <shiftive>;
shiftive: additive SHIFT_OPER shiftive | <additive>;
additive: multitive ADD_OPER additive | <multitive>;
multitive: send MUL_OPER multitive | <unary_op>;

unary_op: [LPAREN] ADD_OPER arg [RPAREN] | <send>;

send: <block> |
      real_send |
      <primary>;

block: real_send "do" (["|"] arglist? ["|"])? line* statement? [LINE_END]? "end";

real_send: primary trailers |
           IDENTIFIER send_args |
           IDENTIFIER [LPAREN] send_args? [RPAREN];

trailers: trailer >trailers< | trailer;

trailer: attribute | subscript;
attribute: [DOT] operation [LPAREN] send_args? [RPAREN] |
           [DOT] operation send_args?;
subscript: ["["] arg ["]"];

primary: LPAREN expr RPAREN |
         "[" args? "]" |
         "if" expr then line* statement? [LINE_END]? "end" |
         "unless" expr then line* statement? [LINE_END]? "end" |
         "while" expr do line* statement? [LINE_END]? "end" |
         "def" IDENTIFIER argdecl line* statement? [LINE_END]? "end" |
         "class" IDENTIFIER (LT IDENTIFIER)? [LINE_END] line* statement? [LINE_END]? "end" |
         varname |
         literal;

then: [LINE_END] |
      "then" |
      [LINE_END] "then";

do: [LINE_END] |
    "do" |
    [LINE_END] "do";

argdecl: [LPAREN] arglist? [RPAREN] |
         arglist? [LINE_END];

arglist: IDENTIFIER [","] >arglist< | IDENTIFIER;

operation: IDENTIFIER | "class" | "end";

send_args: args;

args: arg [","] >args< | arg;

varname: IDENTIFIER |
         "@" IDENTIFIER;

literal: NUMBER | symbol | STRING;

symbol: [":"] IDENTIFIER;